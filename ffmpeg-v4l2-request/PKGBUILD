# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: Paul Mattal <paul@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove makedepends on ffnvcodec-headers, remove --enable-nvenc, --enable-nvdec
#  - remove depends on aom, remove --enable-libaom
#  - remove optdepends on intel-media-sdk, onevpl-intel-gpu
#  - remove depends on onevpl, remove --enable-libvpl
#  - remove depends on vmaf, remove --enable-vmaf
#  - remove depends on rav1e, remove --enable-librav1e
#  - remove depends on svt-av1, remove --enable-libsvtav1
#  - remove --enable-lto

# V4L2-REQUEST
# - Added PKG_FFMPEG_* features
# - Added v4l2-request patches
# Based on information from following pull req: https://github.com/LibreELEC/LibreELEC.tv/pull/9569

AUR_PKG_FFMPEG_SYSTEMWIDE="0"
pkgnamesuffix=-v4l2-request
if [ "${AUR_PKG_FFMPEG_SYSTEMWIDE}" == "0" ]; then
  pkgname=ffmpeg${pkgnamesuffix}
else
  pkgname=ffmpeg
fi
pkgver=7.1
pkgrel=6
epoch=3
pkgdesc='Complete solution to record, convert and stream audio and video'
arch=(x86_64 aarch64)
url=https://ffmpeg.org
license=(GPL-3.0-only)
depends=(
  alsa-lib
  bzip2
  cairo
  dav1d
  fontconfig
  freetype2
  fribidi
  glib2
  glibc
  glslang
  gmp
  gnutls
  gsm
  harfbuzz
  jack
  lame
  libass
  libavc1394
  libbluray
  libbs2b
  libdrm
  libdvdnav
  libdvdread
  libgl
  libiec61883
  libjxl
  libmodplug
  libopenmpt
  libplacebo
  libpulse
  libraw1394
  librsvg
  libsoxr
  libssh
  libtheora
  libva
  libvdpau
  libvorbis
  libvpx
  libwebp
  libx11
  libxcb
  libxext
  libxml2
  libxv
  ocl-icd
  opencore-amr
  openjpeg2
  opus
  rubberband
  sdl2
  snappy
  speex
  srt
  v4l-utils
  vapoursynth
  vid.stab
  vulkan-icd-loader
  x264
  x265
  xvidcore
  xz
  zeromq
  zimg
  zlib
)
makedepends=(
  amf-headers
  avisynthplus
  clang
  frei0r-plugins
  git
  ladspa
  mesa
  nasm
  opencl-headers
  vulkan-headers
)
optdepends=(
  'avisynthplus: AviSynthPlus support'
  'frei0r-plugins: Frei0r video effects support'
  'ladspa: LADSPA filters'
)
options=(
  debug
)
_tag=507a51fbe9732f0f6f12f43ce12431e8faa834b7
source=(
  git+https://git.ffmpeg.org/ffmpeg.git?signed#tag=${_tag}
  add-av_stream_get_first_dts-for-chromium.patch
  ffmpeg-001-v4l2-request.patch
  ffmpeg-001-v4l2-drmprime.patch
  ffmpeg-001-vf-deinterlace-v4l2m2m.patch
  fix_build_with_texinfo-7.2.patch
)
b2sums=('c7ec6b1db61608195117b79f3f0c8f6323c3abeb39721359da0f10e7d739da8301e04ff5fa83c022f86fc760f66e00066f9a50d97b771f797ccc679f9d912c40'
        '555274228e09a233d92beb365d413ff5c718a782008075552cafb2130a3783cf976b51dfe4513c15777fb6e8397a34122d475080f2c4483e8feea5c0d878e6de'
        'bccf4a97a624b2603eb6fc0d4171d5802bfd56f58924015505761e9d9e54444827bda8fa5371b5c4a7b306c35ceeda71d567a63c4a8f176a6c560b2e3ba55008'
        '7027bf633b4b369b5a94b4fe88d5273cbbbb9ff56505dc207914abed8721fe69c580329da90f2f38184db454edc87acbdaae9c2f41d544803d36382a6bbea67b'
        '840f83ab2ffaddca405aeee3349aade71fa6f0276d12592ca6ae1fe7fe95d0ef9312e4cfe7c3f43e1c9d340d173c6689c7e4e5a23b5bdbf4a5fdecf76515cb03'
        '7b33e9527322604532f2b26b5c78680c54d7e50c07ff6acaddd4d17e031724ae91474f46cb034d9d02fb0cf370d6477e1d4f5b04c4dddab5e8b319f03c0fc9b9')
validpgpkeys=(DD1EC9E8DE085C629B3E1846B18E8928B3948D64) # Michael Niedermayer <michael@niedermayer.cc>

PKG_FFMPEG_PREFIX="/usr"
PKG_FFMPEG_RPATH=""
if [ "${AUR_PKG_FFMPEG_SYSTEMWIDE}" == "0" ]; then
  PKG_FFMPEG_PREFIX="/opt/v4l2-request/usr"
  PKG_FFMPEG_RPATH="--enable-rpath"
else
  provides=(
    libavcodec.so
    libavdevice.so
    libavfilter.so
    libavformat.so
    libavutil.so
    libpostproc.so
    libswresample.so
    libswscale.so
    ffmpeg
  )
fi

prepare() {
  cd ffmpeg
  git reset hard
  # Fix build with texinfo-7.2
  # See https://www.linuxquestions.org/questions/slackware-14/texinfo-7-2-looks-to-have-broken-texinfo-convert-html-4175745581/
  # Patch taken from LSF: https://www.linuxfromscratch.org/patches/blfs/svn/ffmpeg-7.1-texinfo_fix-1.patch
  patch -Np1 -i ../fix_build_with_texinfo-7.2.patch
  patch -Np1 -i ../add-av_stream_get_first_dts-for-chromium.patch # https://crbug.com/1251779
  patch -Np1 -i ../ffmpeg-001-v4l2-request.patch
  patch -Np1 -i ../ffmpeg-001-v4l2-drmprime.patch
  patch -Np1 -i ../ffmpeg-001-vf-deinterlace-v4l2m2m.patch
  # VAAPI HEVC encode alignment fix
  git cherry-pick -n bcfbf2bac8f9eeeedc407b40596f5c7aaa0d5b47
  git cherry-pick -n d0facac679faf45d3356dff2e2cb382580d7a521
}

pkgver() {
  cd ffmpeg
  git describe --tags | sed 's/^n//'
}

build() {
  export PKG_CONFIG_PATH='/usr/lib/mbedtls2/pkgconfig'
  cd ffmpeg

  local PKG_FFMPEG_AV1="--enable-libdav1d"
  local PKG_FFMPEG_OPTIMIZATIONS="--enable-optimizations \
                                  --enable-runtime-cpudetect \
                                  --enable-asm \
                                  --enable-lto"
  local PKG_FFMPEG_HWACCEL="--enable-hwaccels"

  local PKG_FFMPEG_V4L2="--enable-libdrm \
                         --enable-libudev \
                         --enable-v4l2_m2m \
                         --enable-v4l2-request"
  local PKG_FFMPEG_FPU=""

  if [ "${CARCH}" == "aarch64" ]; then
    PKG_FFMPEG_FPU="--enable-neon"
  fi

  ./configure \
    --extra-cflags="${CFLAGS} ${LTOFLAGS} -O3" \
    --extra-ldflags="${LDFLAGS} ${LTOFLAGS}" \
    --prefix="${PKG_FFMPEG_PREFIX}" \
    ${PKG_FFMPEG_RPATH} \
    --disable-debug \
    --disable-static \
    --disable-stripping \
    --enable-amf \
    --enable-avisynth \
    --enable-fontconfig \
    --enable-frei0r \
    --enable-gmp \
    --enable-gnutls \
    --enable-gpl \
    --enable-ladspa \
    --enable-libass \
    --enable-libbluray \
    --enable-libbs2b \
    --enable-libdav1d \
    --enable-libdrm \
    --enable-libdvdnav \
    --enable-libdvdread \
    --enable-libfreetype \
    --enable-libfribidi \
    --enable-libglslang \
    --enable-libgsm \
    --enable-libharfbuzz \
    --enable-libiec61883 \
    --enable-libjack \
    --enable-libjxl \
    --enable-libmodplug \
    --enable-libmp3lame \
    --enable-libopencore_amrnb \
    --enable-libopencore_amrwb \
    --enable-libopenjpeg \
    --enable-libopenmpt \
    --enable-libopus \
    --enable-libplacebo \
    --enable-libpulse \
    --enable-librsvg \
    --enable-librubberband \
    --enable-libsnappy \
    --enable-libsoxr \
    --enable-libspeex \
    --enable-libsrt \
    --enable-libssh \
    --enable-libtheora \
    --enable-libv4l2 \
    --enable-libvidstab \
    --enable-libvorbis \
    --enable-libvpx \
    --enable-libwebp \
    --enable-libx264 \
    --enable-libx265 \
    --enable-libxcb \
    --enable-libxml2 \
    --enable-libxvid \
    --enable-libzimg \
    --enable-libzmq \
    --enable-opencl \
    --enable-opengl \
    --enable-shared \
    --enable-vapoursynth \
    --enable-version3 \
    --enable-vulkan $CONFIG \
    ${PKG_FFMPEG_OPTIMIZATIONS} \
    ${PKG_FFMPEG_HWACCEL} \
    ${PKG_FFMPEG_FPU} \
    ${PKG_FFMPEG_V4L2} \
    ${PKG_FFMPEG_AV1}

  make
  make tools/qt-faststart
  make doc/ff{mpeg,play}.1
}

package() {
  depends+=(
    libass.so
    libbluray.so
    libbs2b.so
    libdav1d.so
    libfreetype.so
    libharfbuzz.so
    libjxl.so
    libopenmpt.so
    libplacebo.so
    librsvg-2.so
    librubberband.so
    libva.so
    libva-drm.so
    libva-x11.so
    libvidstab.so
    libvorbisenc.so
    libvorbis.so
    libvpx.so
    libx264.so
    libx265.so
    libxvidcore.so
    libzimg.so
    libzmq.so
    libudev.so
  )

  make DESTDIR="${pkgdir}" -C ffmpeg install install-man
  install -Dm 755 ffmpeg/tools/qt-faststart "${pkgdir}${PKG_FFMPEG_PREFIX}"/bin/
  if [ "${AUR_PKG_FFMPEG_SYSTEMWIDE}" == "0" ]; then
    mkdir -p "${pkgdir}/usr/bin/"
    ln -sf ${PKG_FFMPEG_PREFIX}"/bin/ffmpeg" "${pkgdir}/usr/bin/ffmpeg${pkgnamesuffix}"
    ln -sf ${PKG_FFMPEG_PREFIX}"/bin/ffmpeg_harvid" "${pkgdir}/usr/bin/ffmpeg_harvid${pkgnamesuffix}"

    ln -sf ${PKG_FFMPEG_PREFIX}"/bin/ffplay" "${pkgdir}/usr/bin/ffplay${pkgnamesuffix}"

    ln -sf ${PKG_FFMPEG_PREFIX}"/bin/ffprobe" "${pkgdir}/usr/bin/ffprobe${pkgnamesuffix}"
    ln -sf ${PKG_FFMPEG_PREFIX}"/bin/ffprobe_harvid" "${pkgdir}/usr/bin/ffprobe_harvid${pkgnamesuffix}"
  fi
}

# vim: ts=2 sw=2 et:
